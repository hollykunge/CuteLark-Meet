buildscript {
  repositories {
    jcenter()
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.grails.plugins:hibernate5:${gormVersion - ".RELEASE"}"
    classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.15.1"
  }
}

version "0.10.0"
group "org.bigbluebutton.web"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "org.grails.grails-web"
apply plugin: "asset-pipeline"
apply plugin: "org.grails.grails-gsp"

task resolveDeps(type: Copy) {
  into('lib')
  from configurations.default
  from configurations.default.allArtifacts.file
  doLast { println '[bbb:task] Resolved dependencies for bigbluebutton-web application' }
}

// Task that creates the index file which lists all extra libs
task copyWebInf(type: Copy) {
  into("${project.buildDir}/resources/main/")
  from("src/main/webapp")
}

// make  processResources depends on copyWebInf task
processResources.dependsOn copyWebInf

repositories {
  jcenter()
  mavenLocal()
  maven { url "https://repo.grails.org/grails/core" }
}

configurations {
  compile.exclude group: "org.red5", module: "red5-server-common"
}

dependencies {
  compile "org.springframework.boot:spring-boot-starter-logging"
  compile "org.springframework.boot:spring-boot-autoconfigure"
  compile "org.grails:grails-core"
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-starter-tomcat"
  compile "org.grails:grails-web-boot"
  compile "org.grails:grails-logging"
  compile "org.grails:grails-plugin-rest"
  compile "org.grails:grails-plugin-databinding"
  compile "org.grails:grails-plugin-i18n"
  compile "org.grails:grails-plugin-services"
  compile "org.grails:grails-plugin-url-mappings"
  compile "org.grails:grails-plugin-interceptors"
  compile 'org.grails.plugins:external-config:1.2.2'
  compile "org.grails.plugins:cache"
  compile "org.grails.plugins:async"
  compile "org.grails.plugins:scaffolding"
  compile "org.grails.plugins:events"
  compile "org.grails.plugins:gsp"
  //--- BigBlueButton Dependencies Start - Transitive dependencies have to be re-defined below
  compile "org.bigbluebutton:bbb-common-message_2.12:0.0.20-SNAPSHOT"
  compile "org.bigbluebutton:bbb-common-web:0.0.3-SNAPSHOT"
  compile "io.lettuce:lettuce-core:5.1.3.RELEASE"
  compile "org.reactivestreams:reactive-streams:1.0.2"
  compile "io.projectreactor:reactor-core:3.2.3.RELEASE"
  compile "org.freemarker:freemarker:2.3.28"
  compile "com.google.code.gson:gson:2.8.5"
  compile "org.json:json:20180813"
  compile "org.jodconverter:jodconverter-local:4.3.0"
  compile "com.zaxxer:nuprocess:1.2.4"
  compile "net.java.dev.jna:jna:4.5.1"
  // https://mvnrepository.com/artifact/commons-fileupload/commons-fileupload
  compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4'

  //--- BigBlueButton Dependencies End
  console "org.grails:grails-console"
  profile "org.grails.profiles:web"
  runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.15.1"
  testCompile "org.grails:grails-gorm-testing-support"
  testCompile "org.grails.plugins:geb"
  testCompile "org.grails:grails-web-testing-support"
  testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:2.47.1"
  testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
  testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
  testCompile "com.github.javafaker:javafaker:0.12"
}

bootRun {
  jvmArgs('-Dspring.output.ansi.enabled=always')
  addResources = true
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

assets {
  minifyJs = true
  minifyCss = true
}

test {
  testLogging {
    events = ["passed", "failed", "skipped"]

    // Make sure output from
    // standard out or error is shown
    // in Gradle output.
    showStandardStreams = true
    showExceptions = true
    showCauses = true
    exceptionFormat = "full"
  }
}